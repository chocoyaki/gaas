/****************************************************************************/
/* Defines the componentside interfaces needed by the new LogService        */
/*                                                                          */
/*  Author(s):                                                              */
/*    - Georg Hoesch (hoesch@in.tum.de)                                     */
/*    - Cyrille Pontivieux (cyrille.pontivieux@ens-lyon.fr)                 */
/*                                                                          */
/* $LICENSE$                                                                */
/****************************************************************************/
/* $Id$
 * $Log$
 * Revision 1.3  2010/07/12 15:54:11  glemahec
 * DIET 2.5 beta 1 - Forwarder IDL files
 *
 * Revision 1.2  2007/08/31 16:52:25  bdepardo
 * Implemented the new test() method in LogCentralComponent.
 * Used to check whether or not a component is reachable.
 *
 * Revision 1.1  2004/03/01 18:27:32  rbolze
 * idl file for logservice
 *
 * Revision 1.12  2003/11/18 18:41:14  cpontvie
 * Catch more exceptions
 *
 * Revision 1.11  2003/11/12 12:56:41  cpontvie
 * Bugfixes, more comments, remove some useless sections
 *
 * Revision 1.10  2003/11/06 16:06:32  cpontvie
 * Correct some bugs with mutex and change the type of the log_time_t
 *
 * Revision 1.9  2003/11/03 14:28:48  cpontvie
 * Update all Makefiles and add some checks to the testers
 *
 * Revision 1.8  2003/10/29 13:13:49  cpontvie
 * Add a constant
 *
 * Revision 1.7  2003/10/27 17:49:03  cpontvie
 * Add the implementation for the LogCentralComponent_impl
 *
 * Revision 1.6  2003/10/24 13:28:37  cpontvie
 * Change the log_time_t struct to a typedef of long long and some others minor
 * changes
 *
 * Revision 1.5  2003/10/24 12:05:43  ghoesch
 * corrected minor spelling errors in function names
 *
 * Revision 1.4  2003/10/24 10:05:11  cpontvie
 * New constants definition
 *
 * Revision 1.3  2003/10/23 10:06:44  cpontvie
 * minor changes
 *
 * Revision 1.2  2003/10/22 09:03:59  ghoesch
 * added the correct headers
 * 
 ****************************************************************************/

#ifndef _LOGCOMPONENT_IDL_
#define _LOGCOMPONENT_IDL_

#include "LogTypes.idl"

/* This file defines everything that is needed to attach a component to the
 * LogCentral.
 */

/**
 * Errorlevel constants for connectComponent
 */
// const short LS_OK = 0; defined in the LogTypes.idl
const short LS_COMPONENT_CONNECT_BADNAME = 1;
const short LS_COMPONENT_CONNECT_ALREADYEXISTS = 2;
const short LS_COMPONENT_CONNECT_BADCOMPONENTCONFIGURATOR = 3;
const short LS_COMPONENT_CONNECT_INTERNALERROR = 4;

const short LS_COMPONENT_DISCONNECT_NOTEXISTS = 1;

/**
 * defines functions that the component has to implement 
 * to allow dynamic configuration.
 */
interface ComponentConfigurator {
  /**
   * Set the component filter to the specified taglist. All tags
   * in the list have to be forwarded, all others not.
   */
  void
  setTagFilter(in tag_list_t tagList);

  /**
   * Add the specified taglist to the internal filter and merge them.
   */
  void
  addTagFilter(in tag_list_t tagList);

  /**
   * Remove all tags from the taglist from the internal filter. The
   * given tagList may contain tags that the internal filter does
   * not contain.
   */
  void
  removeTagFilter(in tag_list_t tagList);

  /**
   * Do nothing. Useful to be sure that the object is reachable
   */
  void
  test();
};

/**
 * defines the interface the core offers to components
 */
interface LogCentralComponent {
  /**
   * Do nothing but usefull to be sure that the object is reachable
   */
  void
  test();

  /**
   * Connect a component to the LogCentral. The component attaches with its
   * name, which must be unique (among all components) and its hostname (no
   * need to be unique). If the name is an empty string, a name is generated.
   * The return value indicates if the component could be connected.
   * If ALREADYEXISTS is returned, another component with the given name is
   * registered. In this case, the connection was not successful, and the tool
   * must connect with a diferent name before invoking other functions of the
   * LogCentralComponent interface.
   */
  short
  connectComponent(inout string componentName,
                   in string componentHostname,
                   in string message,
                   in string compConfiguratorName,
                   in log_time_t componentTime,
                   inout tag_list_t initialConfig);


  /**
   * Disconnect a connected component from the LogCentral.
   * No further messages should be sent after disconnection.
   * The componentConfigurator will no longer be used by the LogCentral after
   * this call. Returns NOTCONNECTED if the calling component was not
   * registered.
   */
  short
  disconnectComponent(in string componentName, in string message);


  /**
   * Send a buffer of log_msg_t to the LogCentral.
   */
  oneway void
  sendBuffer(in log_msg_buf_t buffer);


  /**
   * Get the hostname of a specified component.
   */
  string
  getHostnameOf(in string componentName);

  /**
   * To be called by a thread for saying that the component is still alive.
   * If the last ping is too old, the component is considered dead and
   * generate a disconnect message (OUT).
   */
  oneway void
  ping(in string componentName);

  /**
   * To be called by a thread for clock synchronisation.
   */
  oneway void
  synchronize(in string componentName, in log_time_t componentTime);
};

#endif
